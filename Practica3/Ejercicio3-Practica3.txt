program Ej3P3;
Uses crt, SysUtils;
const
  valorAlto = 9999;
type

  novela = record
    codigo : integer;
    genero : string[25];
    nombre : String[25];
    duracion : String[25];
    director : String[25];
    precio : real;
  end;

  archivo = file of novela;

procedure leerNov(var reg : novela);
begin
  write('Ingrese codigo de novela ');
  readln(reg.codigo);
  if(reg.codigo <> -1) then begin
    write('Ingrese genero ');
    readln(reg.genero);
    write('Ingrese nombre ');
    readln(reg.nombre);
    write('Ingrese duracion ');
    readln(reg.duracion);
    write('Ingrese director ');
    readln(reg.director);
    write('Ingrese precio ');
    readln(reg.precio);
  end;
end;

procedure leer(var arch : archivo; var reg : novela);
begin
  if(not eof(arch)) then
    read(arch,reg)
  else
    reg.codigo := valorAlto;
end;

procedure cargarArchivo(var arch : archivo);
var
  reg : novela;
begin
  rewrite(arch);
  reg.codigo := 0;
  write(arch,reg);
  leerNov(reg);
  while(reg.codigo <> -1) do begin
    write(arch,reg);
    leerNov(reg);
  end;
  close(arch);
end;

procedure darAlta(var arch : archivo);
var
  reg, aux : novela;
  pos : integer;
begin
  reset(arch);
  leerNov(reg);
  leer(arch,aux);
  if(aux.codigo < 0) then begin
    pos := aux.codigo*-1;
    seek(arch,pos);
    read(arch,aux);
    seek(arch,filepos(arch)-1);
    write(arch,reg);
    seek(arch,0);
    write(arch,aux);
  end
  else begin
    seek(arch,filesize(arch));
    write(arch,reg);
  end;
  close(arch);
end;

procedure actualizarNovela(var arch : archivo);
var
  reg : novela;
  cod : integer;
  op : String[2];
begin
  write('Ingrese codigo de novela ha modificar ');
  readln(cod);
  reset(arch);
  leer(arch,reg);
  while(reg.codigo <> valorAlto) and (reg.codigo <> cod) do
    leer(arch,reg);
  if(reg.codigo = cod) then begin
    clrscr;
    seek(arch,filepos(arch)-1);
    writeln('1 : modificar genero');
    writeln('2 : modificar nombre');
    writeln('3 : modificar duracion');
    writeln('4 : modificar director');
    writeln('5 : modificar precio');
    readln(op);
    case op of
      '1' : begin
              write('Ingrese nuevo genero ');
              readln(reg.genero);
            end;
      '2' : begin
              write('Ingrese nuevo nombre ');
              readln(reg.nombre);
            end;
      '3' : begin
              write('Ingrese nueva duracion ');
              readln(reg.duracion);
            end;
      '4' : begin
              write('Ingrese nuevo director ');
              readln(reg.director);
            end;
      '5' : begin
              write('Ingrese nuevo precio ');
              readln(reg.precio);
            end;
    end;
    write(arch,reg);
  end;
  close(arch);
end;

procedure darBaja(var arch : archivo);
var
  reg, aux : novela;
  cod, pos : integer;
begin
  write('Ingrese codigo de novela ha eliminar ');
  readln(cod);
  reset(arch);
  leer(arch,aux);
  leer(arch,reg);
  while(reg.codigo <> valorAlto) and (reg.codigo <> cod) do
    leer(arch,reg);
  if(reg.codigo = cod) then begin
    pos := (filepos(arch)-1) *-1;
    seek(arch,filepos(arch)-1);
    write(arch,aux);
    reg.codigo := pos;
    seek(arch,0);
    write(arch,reg);
  end
  else
    writeln('El codigo ingresado no existe');
  close(arch);
end;

procedure mantenimiento(var arch : archivo);
var
  reg : novela;
  op : String[2];
begin
  clrscr;
  writeln('1 : dar alta');
  writeln('2 : actualizar novela');
  writeln('3 : dar baja');
  readln(op);
  case op of
    '1' : darAlta(arch);
    '2' : actualizarNovela(arch);
    '3' : darBaja(arch);
  end;
end;

procedure reporteTxt(var arch : archivo);
var
  reg : novela;
  txt : Text;
begin
  assign(txt,'novelas.txt');
  reset(arch);
  rewrite(txt);
  leer(arch,reg);
  while(reg.codigo <> valorAlto) do begin
    writeln(txt,reg.codigo,' ',reg.genero,' ',reg.nombre,' ',reg.duracion,' ',reg.director,' ',reg.precio:2:2);
    leer(arch,reg);
  end;
  close(arch);
  close(txt);
end;

procedure imprimir(var arch : archivo);
var
  reg : novela;
begin
  reset(arch);
  leer(arch,reg);
  while(reg.codigo <> valorAlto) do begin
    if(reg.codigo > 0) then
      writeln(reg.codigo,' ',reg.genero,' ',reg.nombre,' ',reg.duracion,' ',reg.director,' ',reg.precio:2:2);
    leer(arch,reg);
  end;
  close(arch);
end;

var
  arch : archivo;
  opcion : String[2];
  n : String[25];
begin
  clrscr;

  write('Ingrese nombre del archivo ');
  readln(n);
  assign(arch,n);

  writeln('a : cargar el archivo');
  writeln('b : procesar el archivo');
  readln(opcion);

  case opcion of
    'a' : cargarArchivo(arch);
    'b' : mantenimiento(arch);
    'c' : reporteTxt(arch);
  end;

  writeln;
  imprimir(arch);

  readln;
end.
